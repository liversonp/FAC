#comando para rodar:
#spim -f nome.spim
#Verifica se há overflow
#entradas: um numero entre 0 e 1, em que 0 é unsigned e 1 é signed e 2 valores, a e b
#que serão os números que o programa deve informar se darão overflow ou não.
.data
    cbn: .asciiz "\n"
    overflow: .asciiz "overflow"

.text
main:
    li $v0, 5       #lendo o inteiro i
    syscall
    move $t4, $v0   #passando i para t4

    li $v0, 5       #lendo a variável a
    syscall
    move $t1, $v0   #passando a para t1

    li $v0, 5       #lendo a variável b
    syscall
    move $t2, $v0   #passando b para t2

    bne $t4, $zero, caso_1      #vai para o caso em que i é 1

    nor $t3, $t1, $zero             #faz o nor de a e $zero
    sltu $t3, $t3, $t2              #compara se o valor de 2^32-a é menor que b
    bne $t3, $zero, tem_overflow    #caso sim manda printar overflow

    j sem_overflow

    j exit  #vai para o final do código

    caso_1:
        addu $t0, $t1, $t2              #iniciando o processo para saber se há overflow
        xor $t3, $t1, $t2               #Fazendo o xor para ver se tem sinais iguais
        slt $t3, $t3, $zero             #caso o sinal seja diferente, o resultado dará < 0
        bne $t3, $zero, sem_overflow    #quando os sinais são diferentes, não há overflow

        xor $t3, $t0, $t1               #quando os sinais são iguais, comparamos o sinal de
                                        #uma das variáveis com a do resultado
        slt $t3, $t3, $zero             #se o sinal for diferente, teremos um sinal de que
        bne $t3, $zero, tem_overflow    #houve um overflow e mandamos pro caso.
    
    sem_overflow:
        addu $a0, $t1, $t2
        li $v0, 1
        syscall
        j exit

    tem_overflow:
        li $v0, 4           #vou printar que há overflow
        la $a0, overflow
        syscall

        j exit

    exit:
        li $v0, 4       #printando o \n
        la $a0, cbn
        syscall

        li $v0, 10      #finalizando o programa
        syscall